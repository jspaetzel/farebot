buildscript {
    ext {
        kotlin_version = '2.0.10'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com' }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'app.cash.sqldelight:gradle-plugin:2.0.2'
    }
}

plugins {
    id 'com.google.devtools.ksp' version '2.0.10-1.0.24' apply false
    id 'com.github.ben-manes.versions' version '0.21.0'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com' }
        google()
    }
}

apply from: 'dependencies.gradle'

subprojects {
    apply plugin: 'checkstyle'

    dependencies {
        checkstyle libs.checkstyle
    }

    afterEvaluate {project ->
        if (project.name.contains('farebot')) {
            check.dependsOn 'checkstyle'
            tasks.register('checkstyle', Checkstyle) {
                configFile file('config/checkstyle/checkstyle.xml')
                source 'src'
                include '**/*.java'
                exclude '**/gen/**'
                exclude '**/IOUtils.java'
                exclude '**/Charsets.java'
                classpath = files()
            }
            checkstyle {
                ignoreFailures = false
            }
        }
        if (project.hasProperty("android")) {
            android {
                namespace "dev.meh.farebot"
                compileSdkVersion vers.compileSdkVersion

                defaultConfig {
                    minSdkVersion vers.minSdkVersion
                    targetSdkVersion vers.targetSdkVersion
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_7
                    targetCompatibility JavaVersion.VERSION_1_7
                }

                lintOptions {
                    abortOnError true
                    disable 'InvalidPackage','MissingTranslation'
                }
            }
        }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.31.0'
}

tasks.register('lintKotlin', JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "*/src/**/*.kt"
}
